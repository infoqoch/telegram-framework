buildscript {
    ext {
        queryDslVersion = "5.0.0"
    }
}
plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    //querydsl 추가
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
    id 'java'
}
group = 'infoqoch'
version = '0.2.1'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
repositories {
    mavenCentral()
    mavenLocal()
}
dependencies {
    // telegram bot
    implementation 'infoqoch:telegram-bot:0.2'

    // spring
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // querydsl
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
    annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}"

    // 리플렉션
    implementation 'org.reflections:reflections:0.10.2'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    //테스트에서 lombok 사용
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // poi
    implementation 'org.apache.poi:poi:5.2.2'
    implementation 'org.apache.poi:poi-ooxml:5.2.2'
    implementation 'xerces:xercesImpl:2.12.2' // poi SAXNotRecognizedException 등 parser 관련 해소

    // db
    runtimeOnly 'com.h2database:h2'
    implementation 'org.mariadb.jdbc:mariadb-java-client'

    // 오라클
    implementation 'com.oracle.ojdbc:ucp:19.3.0.0'
    implementation 'com.oracle.database.security:oraclepki'
    implementation 'com.oracle.database.security:osdt_core'
    implementation 'com.oracle.database.security:osdt_cert'
    runtimeOnly ('com.oracle.database.jdbc:ojdbc8-production'){
        exclude group: 'com.oracle.ojdbc', module: 'simplefan'
        exclude group: 'com.oracle.ojdbc', module: 'ons'
    }

    // 로깅
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.0'
    implementation 'org.codehaus.janino:janino'
}
test {
    useJUnitPlatform()
}
//querydsl 추가 시작
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main.java.srcDir querydslDir
}
configurations {
    querydsl.extendsFrom compileClasspath
}
compileQuerydsl {
    clean
    options.annotationProcessorPath = configurations.querydsl
}

compileQuerydsl.doFirst {
    if (file(querydslDir).exists()) delete(file(querydslDir))
}

clean {
    delete file('src/main/generated')
}

tasks.named('compileJava') {
    inputs.files(tasks.named('processResources'))
}

